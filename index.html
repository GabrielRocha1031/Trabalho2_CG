<!DOCTYPE html>
<html>

<head>
    <meta charset="utf-8">
    <title>Ray Marching Example</title>
    <style>
        body {
            display: flex;
            justify-content: center;
            background-color: #0a0a0a;
        }

        canvas {
            width: 96vh;
            height: 96vh;
            
        }
    </style>

</head>

<body>
    <canvas id="canvas"></canvas>
    <script type="x-shader/x-vertex" id="vertexShader">
        precision mediump float;
        attribute vec3 a_position;
        void main() {
          gl_Position = vec4(a_position, 1.0);
        }
    </script>
    <script type="x-shader/x-fragment" id="fragmentShader">
        precision mediump float;
        
        uniform vec2 u_resolution;
        uniform vec3 u_sphereColor;
        uniform vec3 u_cubeColor;
        uniform vec3 u_obstacleColor;
        uniform float u_y;
        uniform float u_x;
        uniform float u_ygota2;
        uniform float u_zCam;

        vec3 lightPosition = vec3(0, 1.4, 1.0);
        vec3 lightColor = vec3(0.3, 0.3, 0.3);
        float lightIntensity =7.0;
        float lightAttenuation = 0.2;

        mat2 rotate(float a) {
            float s = sin(a);
            float c = cos(a);

            return mat2(c, -s, s, c);
        }

        float sphereSDF(vec3 p, vec3 c, float r) {
            return length(p - c) - r;
        }

        float torusSDF( vec3 p, vec2 t, float alt)
        {
        vec2 q = vec2(length(p.xz)-t.x,p.y+alt);
        return length(q)-t.y;
        }


        float cubeSDF(vec3 p, vec3 pCube, vec3 len) {
            vec3 d = abs(vec3(p.x - pCube.x, p.y - pCube.y, p.z - pCube.z)) - len;
            float insideDistance = min(max(d.x, max(d.y, d.z)), 0.0);
            float outsideDistance = length(max(d, 0.0));
            
            return insideDistance + outsideDistance;
        }        
        

        float smoothMax(float a, float b, float k) {
            return log(exp(k * a) + exp(k * b)) / k;
        }
        
        float smoothMin(float a, float b, float k) {
            return -smoothMax(-a, -b, k);
        }

        
                
        vec4 scene(vec3 p) {
            const int len = 12;
            float distances[len];
            vec3 colors[len];

            //chao
            distances[0] = cubeSDF(p, vec3(0.0, -2.0, 0.0), vec3(2.0, 0.01, 2.0));
            
            //direita
            distances[1] = cubeSDF(p, vec3(2.0, 0.0, 0.0), vec3(0.01, 2.0, 2.0));

            //esquerda
            distances[2] = cubeSDF(p, vec3(-2.0, 0.0, 0.0), vec3(0.01, 2.0, 2.0));

            //cima
            distances[3] = cubeSDF(p, vec3(0.0, 2.0, 0.0), vec3(2.0, 0.01, 2.0));
            distances[4] = sphereSDF(p, vec3(0.0, 2.6, 0.0), 0.8);

            //fundo
            distances[5] = cubeSDF(p, vec3(0.0, 0.0, -2.0), vec3(2.0, 2.0, 0.01));

            //bola y
            distances[6] = sphereSDF(p, vec3(0.0, u_y, 0.0), 0.3);
            distances[7] = sphereSDF(p, vec3(0.0, u_y+0.2, 0.0), 0.1);

            //ondas
            distances[8] = torusSDF(p, vec2(u_x, 0.05), 2.0);
            distances[9] = torusSDF(p, vec2(u_x-1.0, 0.02), 2.1);
            distances[10] = torusSDF(p, vec2(u_x-1.8, 0.02), 2.2);

            //respingo
            distances[11] = sphereSDF(p, vec3(0.0, u_ygota2, 0.0), 0.05);


            
            

            //AQUIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIIII

            colors[0] = u_sphereColor;
            colors[1] = u_cubeColor;
            colors[2] = u_cubeColor;
            colors[3] = u_sphereColor;
            colors[4] = u_sphereColor;
            colors[5] = u_cubeColor;
            colors[6] = u_sphereColor;
            colors[7] = u_sphereColor;
            colors[8] = u_obstacleColor;
            colors[9] = u_obstacleColor;
            colors[10] = u_obstacleColor;
            colors[11] = u_obstacleColor;

    

            float d1 = distances[0];
            vec3 color = colors[0];
            float distance = distances[0];
            
            for(int i = 1; i < len; i++) {
                float d2 = distances[i];
                float mix_factor = d1 / (d2 + d1);
                color = mix(
                    color,
                    colors[i],
                    mix_factor
                );
                d1 = mix_factor;
                distance = smoothMin(distance, d2, 7.0);
                //distance = min(distance, d2);
            }

            return vec4(color, distance); 
        }

        vec3 estimateNormal(vec3 p) {
            float eps = 0.001;
            float dx = scene(vec3(p.x + eps, p.y, p.z)).w - scene(vec3(p.x - eps, p.y, p.z)).w;
            float dy = scene(vec3(p.x, p.y + eps, p.z)).w - scene(vec3(p.x, p.y - eps, p.z)).w;
            float dz = scene(vec3(p.x, p.y, p.z + eps)).w - scene(vec3(p.x, p.y, p.z - eps)).w;
            return normalize(vec3(dx, dy, dz));
        }
        
        float calculateLight(vec3 pointPos, vec3 pointNormal) {
            // Calcula a direção da luz em relação ao ponto
            vec3 lightDirection = normalize(lightPosition - pointPos);
          
            // Calcula o produto escalar entre a direção da luz e a normal do ponto
            float diff = max(dot(pointNormal, lightDirection), 0.02);
          
            // Calcula a atenuação da luz em função da distância
            float distance = length(lightPosition - pointPos);
            float attenuation = 1.0 / (1.0 + lightAttenuation * distance * distance);
            
            //calcula a intencidade da specular
            vec3 reflectedDirection = reflect(-lightDirection, pointNormal);
            float cosAngle = max(dot(reflectedDirection, pointNormal), 0.5);
            float specularIntensity = pow(cosAngle, 50.0);

            // Retorna a quantidade de luz que atinge o ponto
            return lightIntensity * attenuation * (diff + specularIntensity);
        }

        float calculateShadow(vec3 pointPos) {
            // Calcula a direção do raio
            vec3 shadowDirection = normalize(lightPosition - pointPos);
            float distanceLength = length(pointPos - lightPosition);
            float radius = 0.1;

            // fraction of light visible, starts at one radius (second half added in the end);
	        float lf = radius * distanceLength;
	
            // distance traveled
            float dt = 0.01;

            for (int i = 0; i < 100; i++) {
                vec3 currentPos = pointPos + shadowDirection * dt;
                float currentDistance = scene(currentPos).w;
                if (currentDistance < -radius) {
                    return 0.0;
                    break;
                }
                
                lf = min(lf, currentDistance / dt);
                dt += currentDistance;

                if (dt > distanceLength) {
                    break;
                }
            }

            lf = clamp((lf*distanceLength + radius) / (2.0 * radius), 0.0, 1.0);
	        lf = smoothstep(0.0, 1.0, lf);
        
            return lf;
        }


        vec3 raymarch(vec3 ro, vec3 rd) {
            float t = 0.0;
            float d = 0.0;  //distance
            vec3 color;
            vec3 reflection;
            float lightAmount;
            float shadow = 1.0;
            float specular = 0.0;
            float shininess = 1000.0; // ajuste a intensidade do brilho
            
            for (int i = 0; i < 100; i++) {
                vec3 p = ro + rd * t;
                vec4 sc = scene(p);
                d = sc.w;
                color = sc.xyz;
                
                t += d;
                if (d < 0.001 || t > 100.0) {
                    
                    vec3 normal = estimateNormal(p);
                    vec3 viewDir = normalize(-rd);
                    vec3 lightDir = normalize(lightPosition - p);
                    vec3 halfwayDir = normalize(lightDir + viewDir);
                    lightAmount = calculateLight(p, normal);
                    shadow = calculateShadow(p);
                    
                    // cálculo da reflexão especular
                    float specularStrength = pow(max(dot(normal, halfwayDir), 0.0), shininess);
                    specular = lightAmount * specularStrength;
                    reflection = mix(color, vec3(1.0), specular); // a cor da reflexão é branca
                    
                    break;
                }
            }
            
            return mix(color, reflection, specular) * lightAmount * lightColor * shadow;
        }

        void main() {
            vec2 uv = gl_FragCoord.xy / u_resolution.xy;
            vec3 pointToLookAt = vec3(0.5, 0.5, 1);
            //ray origin
            vec3 ro = vec3(0.0, 0.0, u_zCam);
            //ray direction
            vec3 rd = normalize(vec3(uv, 0.0) - pointToLookAt);
            vec3 color = raymarch(ro, rd);
            gl_FragColor = vec4(color, 1.0);
        }
    </script>
    <script src="index.js"></script>
</body>

</html>